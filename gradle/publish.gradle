apply plugin: 'net.researchgate.release'
apply plugin: 'maven'
apply from: "./gradle/signing.gradle"

task sourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allJava
}

javadoc.failOnError = false
task javadocJar(type: Jar) {
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: System.getenv('SONATYPE_USERNAME'), password: System.getenv('SONATYPE_PASSWORD'))
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: System.getenv('SONATYPE_USERNAME'), password: System.getenv('SONATYPE_PASSWORD'))
			}

			pom.project {
				name 'Fake Creditcard'
				packaging 'jar'
				description 'Generator of Creditcard Number for Dev tests'
				url 'https://github.com/fakecreditcard/fake-creditcard'

				scm {
					connection 'scm:git://github.com/fakecreditcard/fake-creditcard.git'
					developerConnection 'scm:git://github.com/fakecreditcard/fake-creditcard.git'
					url 'https://github.com/fakecreditcard/fake-creditcard.git'
				}

				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'https://github.com/fakecreditcard/fake-creditcard/blob/master/LICENSE'
					}
				}

				developers {
					developer {
						id 'fernandogodoy'
						name 'Fernando Godóy'
						email 'fernandogodoy18@gmail.com'
					}
				}
			}
		}
	}
}

afterReleaseBuild.dependsOn uploadArchives


apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourceJar(type: Jar) {
	classifier "sources"
	from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier "javadoc"
	from javadoc.destinationDir
}

artifacts {
	archives jar
	archives sourceJar
	archives javadocJar
}

signing {
	sign configurations.archives
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			customizePom(pom)
			groupId 'com.github.fakecreditcard'
			artifactId 'fake-creditcard'
			version '1.0.0'

			from components.java

			pom.withXml {
				def pomFile = file("${project.buildDir}/generated-pom.xml")
				writeTo(pomFile)
				def pomAscFile = signing.sign(pomFile).signatureFiles[0]
				artifact(pomAscFile) {
					classifier = null
					extension = 'pom.asc'
				}
			}

			artifact(sourceJar) {
				classifier = 'sources'
			}
			artifact(javadocJar) {
				classifier = 'javadoc'
			}

			// create the signed artifacts
			project.tasks.signArchives.signatureFiles.each {
				artifact(it) {
					def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
					if (matcher.find()) {
						classifier = matcher.group(1)
					} else {
						classifier = null
					}
					extension = 'jar.asc'
				}
			}

		}
	}
	
	repositories {
		maven {
			url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
			credentials {
				username sonatypeUsername
				password sonatypePassword
			}
		}
	}
}

def customizePom(pom) {
	pom.withXml {
		def root = asNode()

		root.dependencies.removeAll { dep ->
			dep.scope == "test"
		}

		root.children().last() + {
			resolveStrategy = Closure.DELEGATE_FIRST

			description 'Generator of Creditcard Number for Dev tests'
			name 'Fake Creditcard'
			url 'https://github.com/fakecreditcard/fake-creditcard'
			organization {
				name 'fakecreditcard'
				url 'https://github.com/fakecreditcard'
			}
			issueManagement {
				system 'GitHub'
				url 'https://github.com/fakecreditcard/issues'
			}
			licenses {
				license {
					name 'Apache License 2.0'
					url 'https://github.com/fakecreditcard/fake-creditcard/blob/master/LICENSE'
				}
			}
			scm {
				connection 'scm:git://github.com/fakecreditcard/fake-creditcard.git'
				developerConnection 'scm:git://github.com/fakecreditcard/fake-creditcard.git'
				url 'https://github.com/fakecreditcard/fake-creditcard.git'
			}
			developers {
				developer {
					id 'fernandogodoy'
					name 'Fernando Godóy'
					email 'fernandogodoy18@gmail.com'
				}
			}
		}
	}
}

model {
	tasks.generatePomFileForMavenJavaPublication {
		destination = file("$buildDir/generated-pom.xml")
	}

	tasks.publishMavenJavaPublicationToMavenLocal {
		dependsOn project.tasks.signArchives
	}
	tasks.publishMavenJavaPublicationToMavenRepository {
		dependsOn project.tasks.signArchives
	}
}



